buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:8.5.10'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.flywaydb.flyway' version '8.5.10'
}

group = 'io.github.genorchiomento.beer.catalog.infrastructure'
version = '1.0-SNAPSHOT'

bootJar {
    archiveFileName = 'application.jar'
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    //This artifact was moved from implementation("mysql:mysql-connector-java:8.0.33")
    //https://mvnrepository.com/artifact/mysql/mysql-connector-java/8.0.33
    implementation("com.mysql:mysql-connector-j:8.2.0")

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }

    //Undertow tem perfomado melhor que o tomcat
    implementation('org.springframework.boot:spring-boot-starter-undertow')

    implementation('org.springframework.boot:spring-boot-starter-data-jpa')

    testImplementation('org.springframework.boot:spring-boot-starter-test')

    runtimeOnly("com.h2database:h2")
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:mysql://localhost:3306/adm_beer'
    user = System.getenv('FLYWAY_USER') ?: 'root'
    password = System.getenv('FLYWAY_PASS') ?: '123456'
}

test {
    useJUnitPlatform()
}